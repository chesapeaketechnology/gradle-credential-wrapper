buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'net.researchgate:gradle-release:2.6.0'
    }
}

import com.chesapeaketechnology.wrapper.WrapperDownloadTask
apply plugin: 'net.researchgate.release'
apply plugin: 'groovy'
apply plugin: 'maven-publish'

group 'com.chesapeaketechnology.wrapper'

repositories {
    mavenCentral()
}

release {
    failOnPublishNeeded = false
    git {
        requireBranch = 'release'
    }
}

ext {
    // The version of gradle to download should match our version
    gradleVersion = project.version.minus("-SNAPSHOT")
    artifactName = "gradle-credential"
}

dependencies {
    localGroovy()
}

configurations {
    credentialWrapper
}

build.dependsOn('createCredentialWrapper')

task downloadTask (type: WrapperDownloadTask) {
    description 'This task downloads the gradle wrapper version specified by gradleVersion.'
    gradleVersion = project.gradleVersion.toString()
    destinationDir = file("$buildDir/gradle-downloads")
}

task createCredentialWrapper (type: Zip, dependsOn: downloadTask) {
    description 'Download gradle wrapper and add custom init file to distribution'

    // Name for company Gradle ZIP distribution.
    baseName = artifactName
    classifier = 'bin'

    from zipTree(downloadTask.destinationFile)
    into("${downloadTask.distributionNameBase}") {
        into('init.d') {
            from "src/scripts/init.d"
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact createCredentialWrapper
        }
    }
}
