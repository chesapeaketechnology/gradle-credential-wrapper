initscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'nu.studer:gradle-credentials-plugin:1.0.4'
    }
}

allprojects {
    apply plugin: nu.studer.gradle.credentials.CredentialsPlugin

    gradle.taskGraph.whenReady { graph ->
        if (graph.hasTask(updateDistributionUrl) && !project.hasProperty('distributionUrl')) {
            throw new GradleException("When updating the gradle distribution URL, you must specify the new URL via " +
                    "the command line property \"distributionUrl\".\n" +
                    "(ex: -PdistributionUrl=https://services.gradle.org/distributions/gradle-4.4.1-bin.zip)")
        }
    }

    task updateDistributionUrl {
        doLast {
            Properties props = new Properties()
            File propsFile = project.file('gradle/wrapper/gradle-wrapper.properties')
            props.load(propsFile.newDataInputStream())
            props.setProperty('distributionUrl', distributionUrl)
            props.store(propsFile.newWriter(), null)
            println "Gradle wrapper distribution URL set to the following:\n" +
                    "\"$distributionUrl\"\n" +
                    "in properties file:\n" +
                    "\"$propsFile.absoluteFile\"."
        }
    }
}
